/*
 * dataZ - Test Support For Data Stores.
 *
 * Copyright (C) 2014-2016 Marko Umek (http://fail-early.com/contact)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>
 */
apply plugin: 'idea'

version = '0.5.0'

ext {
    dataSetProjects = dataSetProjects.split(/,/)
    destinationDir = targetDir.endsWith('/') ? targetDir : targetDir + '/'
    allProjectDirs = dataSetProjects.inject([]) { result, project ->
        result.add(new File(destinationDir + project))
        result
    }

    logger.debug 'All project directories: ' + allProjectDirs
}

task wrapper(type: Wrapper, description:'Install gradle wrapper') {
  gradleVersion = gradleWrapperVersion
}

repositories {
    mavenCentral()
}


task install(dependsOn: 'installParentGradle', description:'Install dataset projects in ' + destinationDir, group: 'dataSet tasks')

task update(description: 'Update projects and gradle parent', group: 'dataSet tasks') {
    doFirst {
        dataSetProjects.each { project ->
            if( ! cloneProject(new File(destinationDir + project), project) ) {
                updateProject(new File(destinationDir + project), project)
            }
        }
    }
    doLast {
        installGradleProject(destinationDir)
    }
}

task 'show-branches'(description: 'Show branches of dataSet projects', group: 'dataSet tasks') {
    doFirst {
        dataSetProjects.each { project ->
            showBranches(new File(destinationDir + project), project)
        }
    }
}

task 'switch-branch'(description: 'Switch the (existing) branch of dataSet projects', group: 'dataSet tasks') {
    doFirst {
        def branch = checkBrunchParameter(System.getProperty('branch'))
        if( branch ) {
            dataSetProjects.each { project ->
                switchBranch(new File(destinationDir + project), project, branch)
            }
        }
    }
}

def checkBrunchParameter(branch) {
    if (! branch || branch.contains('/')) {
        printErrorMessage(branch)
        return false
    }

    return branch
}


def printErrorMessage(branch) {
    def message = """\
Error: Missing or invalid branch ($branch)!

Usage: gradle switch-branch -Dbranch=<my-branch>

    <my-branch>  Your existing branch (Call gradlew show-branches)

        The branch must not contain remotes/origin/.. or origin/..

    So a valid call might be:

        gradle switch-branch -Dbranch=MyBrunch
"""
        logger.error(message)

}


task cloneProjects(description:'Clone all dataSet projects from github', group: 'internal dataSet task (must not be executed)') {
    outputs.dir allProjectDirs
    doLast {
        dataSetProjects.each { project ->
            cloneProject(new File(destinationDir + project), project)
        }
    }
}

task installParentGradle(dependsOn:'cloneProjects', description:'Create parent project', group: 'internal dataSet task (must not be executed)') {
    outputs.dir allProjectDirs
    doLast {
        installGradleProject(destinationDir)
    }
}

def cloneProject(directory, project) {
    logger.debug("Clone project $project into $directory")
    if (!directory.exists()) {
        def result = exec {
            commandLine "git", "clone", "https://github.com/loddar/${project}.git", directory
        }
        logger.quiet("Cloning '$project' into '$directory' done with $result")
        return true
    } else {
        logger.warn("Project directory '$directory' exists! Cloning skipped!")
        return false
    }
}

def updateProject(directory, project) {
    logger.quiet("Update (git pull) project $project in $directory")
    if (directory.exists()) {
        def result = exec {
            workingDir "$directory"
            commandLine "git", "pull"
        }
        logger.debug("Update '$project' done with $result")
    }
}

def showBranches(directory, project) {
    logger.quiet("Show branches of project '$project':")
    if (directory.exists()) {
        def result = exec {
            workingDir "$directory"
            commandLine "git", "branch", "-a"
        }
        logger.debug("Show branches of project '$project' done with $result")
    }
}

def switchBranch(directory, project, branch) {
    logger.quiet("Switch branch of project '$project' to '$branch'")
    if (directory.exists()) {
        def result = exec {
            workingDir "$directory"
            commandLine "git", "checkout", "$branch"
        }
        logger.debug("Switch branch of project '$project' to '$branch' done with $result")
    }
}



def installGradleProject(directory) {
    logger.quiet "Install parent gradle project in directory '$directory'."
    // gradle build scripts
    def buildScripts=fileTree('src')
    buildScripts.include('*.gradle')
    buildScripts.include('*.properties')
    copy {
        from buildScripts
        into directory
        expand(gradleWrapperVersion: "$gradleWrapperVersion", projects: generateProjects(), version : "$dataSetVersion")
    }
    // gradle wrapper files
    def gradlewFiles=fileTree('.')
    gradlewFiles.include('gradlew')
    gradlewFiles.include('gradlew.bat')
    gradlewFiles.include('gradle/**')
    copy {
        from gradlewFiles
        into directory
    }
}


def generateProjects() {
    def newLine = System.getProperty("line.separator")
    dataSetProjects.inject('') { val, proj ->
        val + "include ':$proj'$newLine"
    }
}
